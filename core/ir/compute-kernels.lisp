;;;; Â© 2016-2018 Marco Heisig - licensed under AGPLv3, see the file COPYING     -*- coding: utf-8 -*-

(in-package :petalisp-ir)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;
;;; Conversion of Subtree Fragments to Kernels
;;;
;;; The goal is to convert a given subtree of a data flow graph to a list
;;; of kernels.  The subtree is delimited by nodes that have a
;;; corresponding entry in the buffer table.  By choosing the iteration
;;; space of our kernels appropriately, we can eliminate all fusion nodes
;;; in the subtree.
;;;
;;; The algorithm consists of two phases.  In the first phase, we compute a
;;; partitioning of the shape of the root into multiple iteration spaces.
;;; These spaces are chosen such that their union is the shape of the root,
;;; but such that each iteration space selects only a single input of each
;;; encountered fusion node.  In the second phase, each iteration space is
;;; used to create one kernel and its body.  The body of a kernel is an
;;; s-expression describing the interplay of applications, reductions and
;;; references.

;;; An immediate node has no kernels.
(defmethod compute-kernels ((root immediate) (backend backend))
  '())

;;; For all non-immediate strided arrays, we create one kernel per
;;; fusion-free iteration space.
(defmethod compute-kernels ((root strided-array) (backend backend))
  (loop for iteration-space in (compute-iteration-spaces root)
        collect
        (make-kernel (compute-kernel-body root iteration-space) backend)))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;
;;; Kernel Iteration Spaces

(defvar *kernel-iteration-spaces*)

(defun compute-iteration-spaces (root)
  (let ((*kernel-iteration-spaces* '()))
    (compute-iteration-spaces-aux
     root
     root
     (shape root)
     (make-identity-transformation (dimension root)))
    ;; The list of iteration spaces generated by COMPUTE-ITERATION-SPACES
    ;; may be empty if there are zero fusion nodes in the subtree.  In this
    ;; case, we return the shape of the root instead.
    (or *kernel-iteration-spaces*
        (list (shape root)))))

;;; Return a boolean indicating whether any of the inputs of NODE, or any
;;; of the inputs thereof, is a fusion node.  Furthermore, whenever NODE is
;;; a fusion node, push a new iteration space for each input that contains
;;; no further fusion nodes.
(defgeneric compute-iteration-spaces-aux
    (root node iteration-space transformation))

(defmethod compute-iteration-spaces-aux :around
    ((root strided-array)
     (node strided-array)
     (iteration-space shape)
     (transformation transformation))
  (if (eq root node)
      (call-next-method)
      (if (nth-value 1 (gethash node *buffer-table*))
          nil
          (call-next-method))))

(defmethod compute-iteration-spaces-aux
    ((root strided-array)
     (fusion fusion)
     (iteration-space shape)
     (transformation transformation))
  ;; Check whether any inputs are free of fusion nodes.  If so, push an
  ;; iteration space.
  (loop for input in (inputs fusion) do
    (let ((subspace (set-intersection iteration-space (shape input))))
      ;; If the input is unreachable, we do nothing.
      (unless (set-emptyp subspace)
        ;; If the input contains fusion nodes, we also do nothing.
        (unless (compute-iteration-spaces-aux root input subspace transformation)
          ;; We have an outer fusion.  This means we have to add a new
          ;; iteration space, which we obtain by projecting the current
          ;; iteration space to the coordinate system of the root.
          (push (transform subspace (invert-transformation transformation))
                *kernel-iteration-spaces*)))))
  t)

(defmethod compute-iteration-spaces-aux
    ((root strided-array)
     (reference reference)
     (iteration-space shape)
     (transformation transformation))
  (compute-iteration-spaces-aux
   root
   (input reference)
   (transform
    (set-intersection iteration-space (shape reference))
    (transformation reference))
   (compose-transformations (transformation reference) transformation)))

(defmethod compute-iteration-spaces-aux
    ((root strided-array)
     (reduction reduction)
     (iteration-space shape)
     (transformation transformation))
  (let* ((range (reduction-range reduction))
         (size (set-size range))
         (iteration-space
           (enlarge-shape
            iteration-space
            (make-range 0 1 (1- size))))
         (transformation
           (enlarge-transformation
            transformation
            (range-step range)
            (range-start range))))
    (loop for input in (inputs reduction)
            thereis
            (compute-iteration-spaces-aux root input iteration-space transformation))))

(defmethod compute-iteration-spaces-aux
    ((root strided-array)
     (application application)
     (iteration-space shape)
     (transformation transformation))
  (loop for input in (inputs application)
          thereis
          (compute-iteration-spaces-aux root input iteration-space transformation)))

(defmethod compute-iteration-spaces-aux
    ((root strided-array)
     (immediate immediate)
     (iteration-space shape)
     (transformation transformation))
  nil)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;
;;; Computing the Kernel Body

(defvar *kernel-root*)

(defvar *kernel-body-statements*)

(defun emit-statement (operator inputs output-type-pairs)
  (push (list* operator inputs output-type-pairs)
        *kernel-body-statements*))

;;; Emit a statement that writes the value of NODE to some location and
;;; return that location.
(defgeneric assign (node iteration-space transformation))

(defun compute-kernel-body (root iteration-space)
  (let ((*kernel-root* root)
        (*kernel-body-statements* '()))
    (if (typep root 'reduction)
        (compute-reduction-kernel-body root iteration-space)
        (compute-standard-kernel-body root iteration-space))))

(defun compute-reduction-kernel-body (root iteration-space)
  (let* ((iteration-space
           (enlarge-shape iteration-space (reduction-range root)))
         (root-transformation
           (make-identity-transformation (dimension root)))
         (body-transformation
           (make-identity-transformation (dimension iteration-space))))
    (loop for input in (inputs root)
          for index from 0 do
            (emit-statement
             'identity
             (list
              (assign input iteration-space body-transformation))
             (list
              (list (reduction-output index) (element-type input)))))
    (list* 'reduction-kernel
           (operator root)
           (nreverse
            (cons (list (gethash root *buffer-table*) root-transformation)
                  (make-list (value-n root) :initial-element nil)))
           iteration-space
           (nreverse *kernel-body-statements*))))

(defun compute-standard-kernel-body (root iteration-space)
  (let* ((transformation
           (make-identity-transformation (dimension iteration-space))))
    (emit-statement
     'identity
     (list
      (assign root iteration-space transformation))
     (list
      (list (list (gethash root *buffer-table*) transformation))))
    (list* 'standard-kernel
           iteration-space
           (nreverse *kernel-body-statements*))))

;; Check whether we are dealing with a leaf, i.e., a node that has a
;; corresponding entry in the buffer table and is not the root node.  If
;; so, return a reference to that buffer.
(defmethod assign :around ((node strided-array)
                           (iteration-space shape)
                           (transformation transformation))
  ;; The root node has an entry in the buffer table, but we do not want to
  ;; process it regardless.
  (if (eq node *kernel-root*)
      (call-next-method)
      (multiple-value-bind (buffer buffer-p)
          (gethash node *buffer-table*)
        (if buffer-p
            (list buffer transformation)
            (call-next-method)))))

(defmethod assign ((application application)
                   (iteration-space shape)
                   (transformation transformation))
  (let ((value-n (value-n application))
        (result (gensym)))
    (emit-statement
     (operator application)
     (loop for input in (inputs application)
           collect
           (assign input iteration-space transformation))
     (nreverse
      (cons (list result (element-type application))
            (make-list value-n :initial-element '(nil t)))))
    result))

(defmethod assign ((reference reference)
                   (iteration-space shape)
                   (transformation transformation))
  (assign
   (input reference)
   (transform
    (set-intersection iteration-space (shape reference))
    (transformation reference))
   (compose-transformations (transformation reference) transformation)))

(defmethod assign ((fusion fusion)
                   (iteration-space shape)
                   (transformation transformation))
  (let ((input (find iteration-space (inputs fusion)
                     :key #'shape
                     :test #'set-intersectionp)))
    (assign
     input
     (set-intersection iteration-space (shape input))
     transformation)))

(defmethod assign ((immediate immediate)
                   (iteration-space shape)
                   (transformation transformation))
  (error "This immediate should have an entry in the buffer table."))
